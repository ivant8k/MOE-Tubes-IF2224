<S> ::= <Program>

<Program> ::= <ProgramHeader> <Block> "."

<ProgramHeader> ::= "program" identifier ";"

<Block> ::= <DeclarationPart> <CompoundStatement>

<DeclarationPart> ::= { <ConstDeclaration>
                      | <TypeDeclaration>
                      | <VarDeclarationPart>
                      | <SubprogramDeclaration> }
                    | ε

<ConstDeclaration> ::= "konstanta" identifier "=" <Expression> ";"

<TypeDeclaration> ::= "tipe" identifier "=" <Type> ";"

<VarDeclarationPart> ::= "variabel" <VarDeclaration> ";" | ε
<VarDeclaration> ::= <IdentifierList> ":" <Type>

<IdentifierList> ::= identifier { "," identifier }

<Type> ::= "integer"
         | "real"
         | "boolean"
         | "char"
         | "string"
         | <ArrayType>
         | identifier

<ArrayType> ::= "larik" "[" <Range> "]" "dari" <Type>

<Range> ::= <Expression> ".." <Expression>

<SubprogramDeclaration> ::= <ProcedureDeclaration> | <FunctionDeclaration>

<ProcedureDeclaration> ::=
      "prosedur" identifier [ "(" <FormalParameterList> ")" ] ";" <Block> ";"

<FunctionDeclaration> ::=
      "fungsi" identifier [ "(" <FormalParameterList> ")" ] ":" <Type> ";" <Block> ";"

<FormalParameterList> ::= <IdentifierList> ":" <Type>
                       { ";" <IdentifierList> ":" <Type> }

<CompoundStatement> ::= "mulai" <StatementList> "selesai"

<StatementList> ::= { <Statement> ";" } | ε

<Statement> ::= <AssignmentStatement>
              | <IfStatement>
              | <WhileStatement>
              | <ForStatement>
              | <ProcedureCall>
              | <CompoundStatement>
              | ε


<AssignmentStatement> ::= identifier ":=" <Expression>
                        | identifier "[" <Expression> "]" ":=" <Expression>

<IfStatement> ::= "jika" <Expression> "maka" <Statement>
                [ "selain-itu" <Statement> ]

<WhileStatement> ::= "selama" <Expression> "lakukan" <Statement>

<ForStatement> ::= "untuk" identifier ":=" <Expression>
                    ( "ke" | "turun-ke" ) <Expression>
                    "lakukan" <Statement>

<ProcedureCall> ::= identifier [ "(" <ParameterList> ")" ]

<ParameterList> ::= <Expression> { "," <Expression> }
